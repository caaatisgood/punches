type GoalId = nat;

type Goal = {
  id: GoalId,
  goal: string,
  tokens: list<nat>,
  active: bool,
};

type TokenData = big_map<nat, {
  goal_id: nat,
  note: string,
  mood: int, // default: -1, 1 ~ 10
}>;

type Storage = {
  admin: address,
  token_count: nat,
  tickets: big_map<nat, ticket<nat>>,
  goals: big_map<address, map<GoalId, Goal>>,
};

type Parameter =
  | ["Set_Goal", string]
  | ["Update_Goal", { goal: string, id: GoalId }]
  | ["Punch", { note: string, id: GoalId }]
  | ["Activate_Goal", GoalId]
  | ["Deactivate_Goal", GoalId];

type Return = [list<operation>, Storage];

const setGoal = (
  { admin, token_count, tickets, goals }: Storage,
  goal: string
): Storage => {
  const sender = Tezos.get_sender();
  const goalMap: map<GoalId, Goal> = match((Big_map.find_opt(sender, goals)), {
    Some: (goalMap: map<GoalId, Goal>) => goalMap,
    None: () => (Map.empty as map<GoalId, Goal>),
  });
  // TODO: check goal length
  const id: nat = Map.size(goalMap);
  const newGoal: Goal = {
    id,
    goal,
    tokens: (list([]) as list<nat>),
    active: true,
  };
  const updatedGoalMap = Map.add(id, newGoal, goalMap);
  const updatedGoals = Big_map.update(
    sender,
    Some(updatedGoalMap),
    goals
  );
  return {
    admin,
    token_count,
    tickets,
    goals: updatedGoals,
  };
};

const updateGoal = (
  { admin, token_count, tickets, goals }: Storage,
  { goal, id }: { goal: string, id: nat }
): Storage => {
  const sender = Tezos.get_sender();
  const goalMap: map<GoalId, Goal> = match((Big_map.find_opt(sender, goals)), {
    Some: (goalMap: map<GoalId, Goal>) => goalMap,
    None: () => failwith("NOT_FOUND"),
  });
  const goalObj: Goal = match((Map.find_opt(id, goalMap)), {
    Some: (goalObj: Goal) => goalObj,
    None: () => failwith("NOT_FOUND"),
  });
  // TODO: check goal length
  const updatedGoalMap = Map.update(id, Some({ ...goalObj, goal }), goalMap);
  const updatedGoals = Big_map.update(sender, Some(updatedGoalMap), goals);
  return { admin, token_count, tickets, goals: updatedGoals };
};

const punch = (
  { admin, token_count, tickets, goals }: Storage,
  { note, id }: { note: string, id: GoalId }
): Storage => {
  const sender = Tezos.get_sender();
  const goalMap: map<GoalId, Goal> = match((Big_map.find_opt(sender, goals)), {
    Some: (goalMap: map<GoalId, Goal>) => goalMap,
    None: () => failwith("NOT_FOUND"),
  });
  const goalObj: Goal = match((Map.find_opt(id, goalMap)), {
    Some: (goalObj: Goal) => goalObj,
    None: () => failwith("NOT_FOUND"),
  });
  // TODO: check note length
  const ticketId: nat = token_count + (1 as nat);
  const ticket_: ticket<nat> = Option.unopt(Tezos.create_ticket(ticketId, 1 as nat));
  const [_, updatedTickets] = Big_map.get_and_update(ticketId, Some(ticket_), tickets);
  const updatedGoalMap = Map.update( id,
    Some({
      ...goalObj,
      tokens: list([ticketId, ...goalObj.tokens]),
    }),
    goalMap
  );
  const updatedGoals = Big_map.update(sender, Some(updatedGoalMap), goals);
  return {
    admin,
    token_count: ticketId,
    tickets: updatedTickets,
    goals: updatedGoals,
  };
};

const activateGoal = (
  { admin, token_count, tickets, goals }: Storage,
  id: GoalId
): Storage => {
  const sender = Tezos.get_sender();
  const goalMap: map<GoalId, Goal> = match((Big_map.find_opt(sender, goals)), {
    Some: (goalMap: map<GoalId, Goal>) => goalMap,
    None: () => failwith("NOT_FOUND"),
  });
  const goalObj: Goal = match((Map.find_opt(id, goalMap)), {
    Some: (goalObj: Goal) => goalObj,
    None: () => failwith("NOT_FOUND"),
  });
  if (goalObj.active) {
    return { admin, token_count, tickets, goals };
  };
  const updatedGoalMap = Map.update(id, Some({ ...goalObj, active: true }), goalMap);
  const updatedGoals = Big_map.update(sender, Some(updatedGoalMap), goals);
  return { admin, token_count, tickets, goals: updatedGoals };
};

const deactivateGoal = (
  { admin, token_count, tickets, goals }: Storage,
  id: GoalId
): Storage => {
  const sender = Tezos.get_sender();
  const goalMap: map<GoalId, Goal> = match((Big_map.find_opt(sender, goals)), {
    Some: (goalMap: map<GoalId, Goal>) => goalMap,
    None: () => failwith("NOT_FOUND"),
  });
  const goalObj: Goal = match((Map.find_opt(id, goalMap)), {
    Some: (goalObj: Goal) => goalObj,
    None: () => failwith("NOT_FOUND"),
  });
  if (!goalObj.active) {
    return { admin, token_count, tickets, goals };
  };
  const updatedGoalMap = Map.update(id, Some({ ...goalObj, active: false }), goalMap);
  const updatedGoals = Big_map.update(sender, Some(updatedGoalMap), goals);
  return { admin, token_count, tickets, goals: updatedGoals };
};

const main = (parameter: Parameter, store: Storage): Return => {
  return [
    list([]),
    (match (parameter, {
      Set_Goal: (goal: string) => setGoal(store, goal),
      Update_Goal: (params) => updateGoal(store, params),
      Punch: (params) => punch(store, params),
      Activate_Goal: (goal_id: GoalId) => activateGoal(store, goal_id),
      Deactivate_Goal: (goal_id: GoalId) => deactivateGoal(store, goal_id),
    }))
  ];
};
