#import "errors.jsligo" "Errors"

type WipId = nat;

type Wip = {
  id: WipId,
  wip: string,
  punch_ids: list<nat>,
  active: bool,
  set_at: timestamp,
};

type WipMap = map<WipId, Wip>;

type Punch = {
  id: nat,
  wip_id: nat,
  note: string,
  punched_at: timestamp,
};

type Storage = {
  punch_count: nat,
  punches: big_map<nat, Punch>,
  tickets: big_map<nat, ticket<nat>>,
  wips: big_map<address, WipMap>,
  wip_chars: nat, // prob ~100
  note_chars: nat, // prob ~50
  house_set: set<string>, // "erevald" | "gaudmire" | "alterok" | "spectreseek"
  admin: address,
  paused: bool,
};

type Parameter =
  // permissionless
  | ["Genesis_wip", { wip: string, house: string }]
  | ["Set_wip", string]
  | ["Update_wip", { wip: string, id: WipId }]
  | ["Punch", { note: string, id: WipId }]
  | ["Activate_wip", WipId]
  | ["Deactivate_wip", WipId]
  // admin
  | ["Set_wip_chars", nat]
  | ["Set_note_chars", nat]
  | ["Set_pause", bool];

type Return = [list<operation>, Storage];

const _insertWip = (wip: string, wipMap): WipMap => {
  const id: nat = Map.size(wipMap);
  const newWip: Wip = {
    id,
    wip,
    punch_ids: (list([]) as list<nat>),
    active: true,
    set_at: Tezos.get_now(),
  };
  return Map.add(id, newWip, wipMap);
};

const genesisWip = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  { wip, house }: { wip: string, house: string }
) => {
  assert_with_error(!paused, Errors.kt_paused);
  assert_with_error(String.length(wip) <= wip_chars, Errors.wipExceedsMaxLen);
  assert_with_error(Set.mem(house, house_set), Errors.invalidHouse);
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => failwith(Errors.hasGenesisWipAlready),
    None: () => (Map.empty as WipMap),
  });
  const updatedWipMap = _insertWip(wip, wipMap);
  const updatedWips = Big_map.update(
    sender,
    Some(updatedWipMap),
    wips
  );
  return { punch_count, punches, tickets, wips: updatedWips, admin, wip_chars, note_chars, house_set, paused };
};

const setWip = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  wip: string
): Storage => {
  assert_with_error(!paused, Errors.kt_paused);
  assert_with_error(String.length(wip) <= wip_chars, Errors.wipExceedsMaxLen);
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.hasNoGenesisWip),
  });
  const updatedWipMap = _insertWip(wip, wipMap);
  const updatedWips = Big_map.update(
    sender,
    Some(updatedWipMap),
    wips
  );
  return { punch_count, punches, tickets, wips: updatedWips, admin, wip_chars, note_chars, house_set, paused };
};

const updateWip = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  { wip, id }: { wip: string, id: nat }
): Storage => {
  assert_with_error(!paused, Errors.kt_paused);
  assert_with_error(String.length(wip) <= wip_chars, Errors.wipExceedsMaxLen);
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
  const wipObj: Wip = match((Map.find_opt(id, wipMap)), {
    Some: (wipObj: Wip) => wipObj,
    None: () => failwith(Errors.not_found),
  });
  const updatedWipMap = Map.update(id, Some({ ...wipObj, wip }), wipMap);
  const updatedWips = Big_map.update(sender, Some(updatedWipMap), wips);
  return { punch_count, punches, tickets, wips: updatedWips, admin, wip_chars, note_chars, house_set, paused };
};

const punch = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  { note, id }: { note: string, id: WipId }
): Storage => {
  assert_with_error(!paused, Errors.kt_paused);
  assert_with_error(String.length(note) <= note_chars, Errors.noteExceedsMaxLen);
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
  const wipObj: Wip = match((Map.find_opt(id, wipMap)), {
    Some: (wipObj: Wip) => wipObj,
    None: () => failwith(Errors.not_found),
  });
  assert_with_error(wipObj.active, Errors.wipInactive);
  const punchId: nat = punch_count;
  const ticket_: ticket<nat> = Option.unopt(Tezos.create_ticket(punchId, 1 as nat));
  const [_, updatedTickets] = Big_map.get_and_update(punchId, Some(ticket_), tickets);
  const updatedWipMap = Map.update(id,
    Some({
      ...wipObj,
      punch_ids: list([punchId, ...wipObj.punch_ids]),
    }),
    wipMap
  );
  const updatedWips = Big_map.update(sender, Some(updatedWipMap), wips);
  const punch: Punch = {
    id: punchId,
    wip_id: id,
    note,
    punched_at: Tezos.get_now(),
  };
  const updatedPunches = Big_map.add(punchId, punch, punches);
  return {
    punch_count: punchId + (1 as nat),
    punches: updatedPunches,
    tickets: updatedTickets,
    wips: updatedWips,
    admin,
    wip_chars,
    note_chars,
    house_set,
    paused,
  };
};

const activateWip = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  id: WipId
): Storage => {
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
  const wipObj: Wip = match((Map.find_opt(id, wipMap)), {
    Some: (wipObj: Wip) => wipObj,
    None: () => failwith(Errors.not_found),
  });
  if (wipObj.active) {
    return { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused };
  }
  const updatedWipMap = Map.update(id, Some({ ...wipObj, active: true }), wipMap);
  const updatedWips = Big_map.update(sender, Some(updatedWipMap), wips);
  return { punch_count, punches, tickets, wips: updatedWips, admin, wip_chars, note_chars, house_set, paused };
};

const deactivateWip = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  id: WipId
): Storage => {
  const sender = Tezos.get_sender();
  const wipMap: WipMap = match((Big_map.find_opt(sender, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
  const wipObj: Wip = match((Map.find_opt(id, wipMap)), {
    Some: (wipObj: Wip) => wipObj,
    None: () => failwith(Errors.not_found),
  });
  if (!wipObj.active) {
    return { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused };
  }
  const updatedWipMap = Map.update(id, Some({ ...wipObj, active: false }), wipMap);
  const updatedWips = Big_map.update(sender, Some(updatedWipMap), wips);
  return { punch_count, punches, tickets, wips: updatedWips, admin, wip_chars, note_chars, house_set, paused };
};

const setWipChars = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  chars: nat
): Storage => {
  assert_with_error((Tezos.get_sender() == admin), Errors.notAllowed);
  return { punch_count, punches, tickets, wips, admin, wip_chars: chars, note_chars, house_set, paused };
};

const setNoteChars = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  chars: nat
): Storage => {
  assert_with_error((Tezos.get_sender() == admin), Errors.notAllowed);
  return { punch_count, punches, tickets, wips, admin, wip_chars, note_chars: chars, house_set, paused };
};

const setPause = (
  { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused }: Storage,
  paused_: bool
) => {
  assert_with_error((Tezos.get_sender() == admin), Errors.notAllowed);
  return { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused: paused_ };
}

const main = (parameter: Parameter, store: Storage): Return => {
  return [
    list([]),
    (match(parameter, {
      Genesis_wip: (params) => genesisWip(store, params),
      Set_wip: (wip: string) => setWip(store, wip),
      Update_wip: (params) => updateWip(store, params),
      Punch: (params) => punch(store, params),
      Activate_wip: (wip_id: WipId) => activateWip(store, wip_id),
      Deactivate_wip: (wip_id: WipId) => deactivateWip(store, wip_id),
      Set_wip_chars: (chars: nat) => setWipChars(store, chars),
      Set_note_chars: (chars: nat) => setNoteChars(store, chars),
      Set_pause: (paused: bool) => setPause(store, paused),
    }))
  ];
};

// @view
const punch_exist = (id: nat, store: Storage): bool => {
  const { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused } = store;
  return id < punch_count;
};

// @view
const get_punch = (id: nat, store: Storage): Punch => {
  const { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused } = store;
  return match((Big_map.find_opt(id, punches)), {
    Some: (punch: Punch) => punch,
    None: () => failwith(Errors.not_found),
  });
};

// @view
const get_wips = (addr: address, store: Storage): WipMap => {
  const { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused } = store;
  return match((Big_map.find_opt(addr, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
};

// @view
const get_wip = (
  { addr, id }: { addr: address, id: WipId },
  store: Storage
): Wip => {
  const { punch_count, punches, tickets, wips, admin, wip_chars, note_chars, house_set, paused } = store;
  const wipMap: WipMap = match((Big_map.find_opt(addr, wips)), {
    Some: (wipMap: WipMap) => wipMap,
    None: () => failwith(Errors.not_found),
  });
  return match((Map.find_opt(id, wipMap)), {
    Some: (wipObj: Wip) => wipObj,
    None: () => failwith(Errors.not_found),
  });
};
